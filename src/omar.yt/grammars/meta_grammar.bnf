:start ::= statements
statements ::= statement+
statement ::= <start rule> color => red
  | <priority rule> color => green
  | <quantified rule> color => blue
  | <discard rule> color => darkgray
  | <empty rule> color => darkgray
  | <comment> color => gray

<start rule> ::= ':start' <op declare bnf> <single symbol>
<priority rule> ::= lhs <op declare> priorities
<quantified rule> ::= lhs <op declare> <single symbol> quantifier <adverb list>
<discard rule> ::= ':discard' <op declare match> <single symbol>
<empty rule> ::= lhs <op declare> <adverb list>

priorities ::= alternatives+
    separator => '||' proper => 1
alternatives ::= alternative+
    separator => '|' proper => 1
alternative ::= rhs <adverb list> bgcolor => beige

<adverb list> ::= <adverb item>* color => cadetblue
<adverb item> ::= <separator specification>
  | <proper specification>
  | <rank specification>
  | <color specification>
  | <bgcolor specification>

<separator specification> ::= 'separator' '=>' <single symbol>
  | 'separator' '=>' <single quoted string>
<proper specification> ::= 'proper' '=>' boolean
<rank specification> ::= 'rank' '=>' digit
<color specification> ::= 'color' '=>' color 
<bgcolor specification> ::= 'bgcolor' '=>' color 

color ::= <hex color>
  | <css color>
<hex color> ~ /#[a-fA-F0-9]{6}/
<css color> ~ [\w]+

lhs ::= symbol
rhs ::= <rhs primary>+
<rhs primary> ::= <single symbol>
  | <single quoted string>
  | <parenthesized rhs primary list>
<parenthesized rhs primary list> ::= '(' <rhs list> ')'
<rhs list> ::= <rhs primary>+

<single symbol> ::= symbol
  | <character class> color => turquoise
  | <regex> color => turquoise
symbol ::= <symbol name>
<symbol name> ::= <bare name>
  | <bracketed name>

:discard ~ whitespace
whitespace ~ [\s]+

<op declare> ::= <op declare bnf> | <op declare match>

<op declare bnf> ~ '::='
<op declare match> ~ '~'
quantifier ::= '*' | '+'

<name> ~ [\w]+
boolean ~ [01]
digit ~ [\d]+
<before or after> ::= 'before' | 'after'

<bare name> ~ [\w]+
<bracketed name> ~ '<' <bracketed name string> '>'
<bracketed name string> ~ [\s\w]+

<single quoted string> ::= <quoted string> color => peru
<quoted string> ~ /'[^'\x0A\x0B\x0C\x0D\x{0085}\x{2028}\x{2029}]+'/

<regex> ~ /\/.*\/[imx]{0,3}/
<character class> ~ '[' <cc elements> ']'
<cc elements> ~ [^\x5d\x0A\x0B\x0C\x0D\x{0085}\x{2028}\x{2029}]+

# Allow comments
<comment> ::= <hash comment>
<hash comment> ~ /#[^\n]*/
